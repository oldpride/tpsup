#!/usr/bin/perl

use warnings;
use strict;
use Data::Dumper;
use Getopt::Long;

my $prog = $0; $prog =~ s:.*/::;

my $delimiter = ":";

sub usage {
   my ($message) = @_;

   print "$message\n" if $message;

   print STDERR <<"END";
Usage:

   $0 path1:path2:...

   reduce the path by removing the duplicates

   -v          verbose mode
   -q          quiet
   -d string   delimiter. defaut to "$delimiter". but for PYTHONPATH in Cygwin, it is semi-colon ;

Examples:

   $0 path1::path2::path3:path1:path2
   path1::path2:path3

   $0 "\$PATH"
   $0 "\$LD_LIBRARY_PATH"

END

   exit 1;
}

my $verbose;
my $quiet;

GetOptions(
   'v|verbose' => \$verbose,
   'q|quiet'   => \$quiet,
   'd=s'       => \$delimiter,
   ) || usage ("cannot parse command line: $!");
usage("wrong number of args") if @ARGV != 1;

my ($old_path) = @ARGV;

print STDERR "old_path=$old_path\n" if !$quiet;

my @old = split /$delimiter/, $old_path;

my $seen;

my @new;

my $i=0;

for my $p (@old) {
   if ($seen->{$p}) {
      print STDERR "dropping duplicate at $i: '$p'\n" if !$quiet;
   } else {
      $seen->{$p}++;
      push @new, $p;
   }

   $i++;
}

my $new_path = join($delimiter, @new);

print STDERR "new_path=$new_path\n" if !$quiet;

print "$new_path\n";

exit 0;
